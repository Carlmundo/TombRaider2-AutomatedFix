; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#define AppName "Tomb Raider 2 Automated Fix"
#define AppVersion "1.0"
#define Game "Tomb Raider II"
#define Game2 "Tomb Raider II Gold (Full Net)"
#define RegkeyCore "Software\Core Design\"
[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{74A59FDF-54AB-4B6D-B80E-99380F49BD1F}
AppName={#AppName}
AppVersion={#AppVersion}
DefaultDirName={code:GetDefaultDir}
DisableProgramGroupPage=yes
DirExistsWarning=no
DisableDirPage=no
AppendDefaultDirName=no
DisableReadyPage=no
AlwaysShowDirOnReadyPage=yes
CloseApplications=yes
OutputBaseFilename=TR2AF {#AppVersion}
SetupIconFile=TR2.ico
WizardImageFile=TR2_Large.bmp
WizardImageStretch=no
WizardSmallImageFile=TR2_small.bmp
Compression=none
SolidCompression=no
Uninstallable=no
PrivilegesRequired=admin

[Files]
Source: ".\Install\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs overwritereadonly

[Registry]
//Set resolution
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game}\System"; ValueType: dword; ValueName: "FullScreenWidth"; ValueData: {code:GetResolution|X}
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game}\System"; ValueType: dword; ValueName: "FullScreenHeight"; ValueData: {code:GetResolution|Y}
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game2}\System"; ValueType: dword; ValueName: "FullScreenWidth"; ValueData: {code:GetResolution|X}
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game2}\System"; ValueType: dword; ValueName: "FullScreenHeight"; ValueData: {code:GetResolution|Y}

//Correct default volume
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game}\Game"; ValueType: dword; ValueName: "MusicVolume"; ValueData: 10
Root: HKCU; Subkey: "{#RegkeyCore}\{#Game2}\Game"; ValueType: dword; ValueName: "MusicVolume"; ValueData: 10

//Set main game compatibility for Windows XP (SP2)
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: String; ValueName: "{app}\Tomb2.exe"; ValueData: "WINXPSP2"

[Code]
//Use SystemMetrics to get resolution
function GetSystemMetrics (nIndex: Integer): Integer;
external 'GetSystemMetrics@User32.dll stdcall setuponly';
Const
    SM_CXSCREEN = 0; // The enum-value for getting the width of the cient area for a full-screen window on the primary display monitor, in pixels.
    SM_CYSCREEN = 1; // The enum-value for getting the height of the client area for a full-screen window on the primary display monitor, in pixels.
function GetResolution(Param: string): string;
begin
  case UpperCase(Param[1]) of
    'X': Result := IntToStr(GetSystemMetrics(SM_CXSCREEN));
    'Y': Result := IntToStr(GetSystemMetrics(SM_CYSCREEN));
  end;
end;

function GetDefaultDir(def: string): string;
var InstalledDir, PathSteam, PathGOG : string;
begin
  PathSteam := 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Steam App 225300';  
  PathGOG := 'SOFTWARE\GOG.com\Games\1207663483';
  if RegQueryStringValue(HKLM, PathSteam, 'InstallLocation', InstalledDir) then
  begin
  end
  else if RegQueryStringValue(HKLM32, PathSteam, 'InstallLocation', InstalledDir) then
  begin
  end
  else if RegQueryStringValue(HKLM64, PathSteam, 'InstallLocation', InstalledDir) then
  begin
  end
  else if RegQueryStringValue(HKLM, PathGOG, 'path', InstalledDir) then
  begin
  end
  else if RegQueryStringValue(HKLM32, PathGOG, 'path', InstalledDir) then
  begin
  end
  else if RegQueryStringValue(HKLM64, PathGOG, 'path', InstalledDir) then
  begin
  end;    
  Result := InstalledDir;      
end;

function NextButtonClick(PageId: Integer): Boolean;
begin
    Result := True;
    if (PageId = wpSelectDir) and (
    not FileExists(ExpandConstant('{app}\tomb2.exe'))) then
    begin
        MsgBox('The Steam or GOG version of {#Game} could not be found in that folder. If it is the correct folder, please try reinstalling the game.', mbError, MB_OK);
        Result := False;
    end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin 
      //Start Post Install
      //Set default resolution for voodoo wrapper in config files     
      SaveStringToFile(ExpandConstant('{app}\dgVoodoo.conf'), 'Resolution = h:' + GetResolution('X') + ', v:' + GetResolution('Y'), True);
      SaveStringToFile(ExpandConstant('{app}\gold\dgVoodoo.conf'), 'Resolution = h:' + GetResolution('X') + ', v:' + GetResolution('Y'), True);
      //End Post Install
    end;
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
SetupAppTitle={#AppName} {#AppVersion}
SetupWindowTitle={#AppName} {#AppVersion} 
WelcomeLabel1={#AppName} {#AppVersion} 
WelcomeLabel2=Before continuing, please make sure that you have a clean install of the {#Game} Steam or GOG release.
SelectDirLabel3=Setup will detect where the Steam version of {#Game} has been installed.
SelectDirBrowseLabel=If it has not been detected, click Browse to specify the folder.
FinishedHeadingLabel=Patch Complete

[InstallDelete]
Type: files; Name: "{app}\audiere.dll"
Type: files; Name: "{app}\data\_INST32I.EX_"
Type: files; Name: "{app}\data\_ISDEL.EXE"
Type: files; Name: "{app}\data\_setup.dll"
Type: files; Name: "{app}\data\_sys1.cab"
Type: files; Name: "{app}\data\_user1.cab"
Type: files; Name: "{app}\data\autorun.exe"
Type: files; Name: "{app}\data\autorun.ico"
Type: files; Name: "{app}\data\autorun.inf"
Type: files; Name: "{app}\data\data"
Type: files; Name: "{app}\data\DATA.TAG"
Type: files; Name: "{app}\data\data1.cab"
Type: files; Name: "{app}\data\dsetup.dll"
Type: files; Name: "{app}\data\dsetup16.dll"
Type: files; Name: "{app}\data\dsetup32.dll"
Type: files; Name: "{app}\data\lang.dat"
Type: files; Name: "{app}\data\language.dat"
Type: files; Name: "{app}\data\layout.bin"
Type: files; Name: "{app}\data\os.dat"
Type: files; Name: "{app}\data\setup.BMP"
Type: files; Name: "{app}\data\SETUP.EXE"
Type: files; Name: "{app}\data\SETUP.INI"
Type: files; Name: "{app}\data\setup.ins"
Type: files; Name: "{app}\data\setup.lid"
Type: files; Name: "{app}\libogg-0.dll"
Type: files; Name: "{app}\libvorbis-0.dll"
Type: files; Name: "{app}\libvorbisfile-3.dll"
Type: files; Name: "{app}\msvcp90.dll"
Type: files; Name: "{app}\msvcr90.dll"
Type: files; Name: "{app}\music\*.mp3"
Type: files; Name: "{app}\music\*.ogg"
Type: files; Name: "{app}\sound.dll"
Type: files; Name: "{app}\tombpc.dat"
